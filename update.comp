#version 450

#include "config.hpp"
#include "random.glsl"

layout(local_size_x = UPDATE_THREADS, local_size_y = UPDATE_THREADS) in;
layout(set = 0, binding = 0, r8ui) uniform readonly uimage2D readImage;
layout(set = 1, binding = 0, r8ui) uniform uimage2D writeImage;
layout(set = 2, binding = 0) uniform uniformOffset
{
    uint offset;
};
layout(set = 2, binding = 1) uniform uniformTime
{
    int time;
};

const ivec2 Offsets[4] = ivec2[](
    ivec2(0, 0),
    ivec2(0, 1),
    ivec2(1, 1),
    ivec2(1, 0)
);

bool contains(ivec2 position)
{
    return position.x >= 0 && position.y >= 0 && position.x < WIDTH && position.y < HEIGHT;
}

bool empty(ivec2 position)
{
    return contains(position) && imageLoad(readImage, position).x == EMPTY;
}

void main()
{
    ivec2 position = ivec2(gl_GlobalInvocationID).xy * 2 + Offsets[offset % 4];
    if (!contains(position))
    {
        return;
    }
    uint particle = imageLoad(readImage, position).x;
    switch (particle)
    {
    case EMPTY:
    case STONE:
        {
            /* noop */
            break;
        }
    case SAND:
        {
            int neighborDirection;
            if (random(ivec3(position, time)) > 0.5f)
            {
                neighborDirection = -1;
            }
            else
            {
                neighborDirection = 1;
            }
            ivec2 neighbors[3] = ivec2[](
                position + ivec2(0, 1),
                position + ivec2(-neighborDirection, 1),
                position + ivec2(neighborDirection, 1)
            );
            for (int i = 0; i < 3; i++)
            {
                if (empty(neighbors[i]))
                {
                    imageStore(writeImage, neighbors[i], uvec4(particle));
                    return;
                }
            }
            imageStore(writeImage, position, uvec4(particle));
            break;
        }
    case WATER:
        {
            /* TODO: */
            break;
        }
    }
}